#!/usr/bin/env raku

# XXX SSIMs are unusually low for images/
# XXX call this script from a Makefile and maintain a text file of SSIMs for each shader
# XXX add option to test multiple planes at a time? (sounds like a pain)
# XXX make a script for plotting stats files
# XXX sometimes ffmpeg hangs for a bit and then spits out a very high inaccurate SSIM

use MONKEY-SEE-NO-EVAL;
use soft;

my %gbl;
my %tmp;
END { %tmp.values.flat.map(&unlink); rmdir %gbl<tmpdir> with %gbl<tmpdir>; };

my $ffmpeg = "ffmpeg -nostdin -y";
# drop stderr and/or stdout if they aren't requested
# die and print stdout & stderr if command fails
# uses timeout(1) w/ retries to ensure process doesn't hang
&run.wrap(sub (*@args, *%opts --> Proc) {
	%opts<out> = True without %opts<out>;
	%opts<err> = True without %opts<err>;
	note(@args) if %gbl<verbose>;
	@args = (|<timeout -k 5 90>, |@args);

	my $proc;
	loop (my $i = 0; $i < 5; $i++) {
		given $proc = callwith(|@args, |%opts) {
		# timeout can return different exit codes & it shouldn't hurt to be wrong here
		# for some reason run might return Any when timed out
		when $proc === Any || $_.exitcode > 120 { note("ugh: looks like a command hung, trying again..."); }
		when $_.exitcode == 0 { return $_; }
		default { last; }
		}
	}

	with $proc {
		note($proc.out.slurp(:close)) with $proc.out;
		note($proc.err.slurp(:close)) with $proc.err;
	}
	die 'command failed: ' ~ @args;
});

# GLSL macro-safe number representation
multi sub msafe(Int $x --> Str()) { $x }
multi sub msafe(Numeric $x --> Str) { sprintf('%f', $x.FatRat) }
multi sub msafe($x --> Str()) { $x }

# parses output from ffmpeg's SSIM filter and outputs an average
# XXX RGB testing is broken (not sure if it's here or somewhere else)
sub avgssim(Str $out --> FatRat) {
	$out.note if %gbl<verbose>;
	my %avg;
	$out ==> split("\n", :skip-empty)
	     ==> map({ .split(' ')[1..*-2]>>.split(':').flat.hash })
	     ==> { $_>>.FatRat }() # better precision
	     ==> { [>>+<<] $_ >>/>> .elems }() # hash average
	     ==> %avg;
	
	given %gbl<plane> {
	when 'LUMA' { return %avg<Y>; }
	when 'CHROMA' { return (%avg<U> + %avg<V>)/2; }
	default { return ([+] %avg.values) / %avg.values.elems; }
	}
}

# dispatches the appropriate cfg script (if any) w/ $cfgopts
# returns the path to the new shader
# XXX allow custom cfg scripts to be dispatched?
sub run_cfg(Str $cfgopts --> Str) {
	return %gbl<shader> if !$cfgopts && %gbl<shader>.IO.basename !~~ /_template$/;

	my $out-shader;
	loop { # ensure a unique filename
		try { $out-shader = open("{%gbl<tmp>}.{999.rand.Int}.glsl", :rx); }
		last if not $!;
		note($!) if %gbl<verbose>;
	}
	my $path = $out-shader.IO.path;
	LEAVE { $out-shader.close with $out-shader; }

	run <<./shader_cfg "{%gbl<shader>}" "OPTS=CFG_HOOKS={%gbl<plane>}\:$cfgopts">>, :out($out-shader);

	%tmp{$path} = $path;
}

sub shader_ssim(Str $in1, Str $in2, Str $shader --> Str) {
	my $vf = "hwupload,libplacebo={%gbl<resolution>.split(':')[0..1].join(':')}\:{%gbl<pix-fmt>}:custom_shader_path=$shader,hwdownload[placebo]; [placebo]ssim=-";
	run(<<$ffmpeg -i "$in1" -i "$in2" -init_hw_device "%gbl<vk>" -lavfi "$vf" -f null ->>, :out).out.slurp(:close);
}

sub stats($cfgopts, $ssim, :$erronly = False) {
	my $s = "{$cfgopts || 'NO_CFGOPTS'} $ssim";
	if $erronly {
		note($s);
	} else {
		print("$s\n");
		%gbl<statsfile>.print("$s\n") if %gbl<statsfile>;
		%gbl<statsfile>.flush if %gbl<statsfile>;
	}
	return;
}

# runs ffmpeg for testing, optionally maximizes a parameter
# XXX maybe break the maximization logic off into a separate function
# XXX refactor to loop instead of recursing
sub test(Str $cfgopts = "", :@history = [], *%state_ --> Pair) {
	my %state = %state_; # for mutating

	# set default values
	my ($m_opt, $m_start, $m_step, $m_epsilon, $m_iter) = |%gbl<max>;
	%state<value> = %state<value> || $m_start;
	%state<step> = %state<step> || $m_step;
	%state<iter> = $m_opt ?? (%state<iter> || $m_iter) !! 1;
	%state<depth> = %state<depth> || 0;
	%state<sandtrap> = %state<sandtrap> || 0;

	my Pair $prev = @history.head || (Nil=>0.0);
	my &max = { @history.sort(*.value)[*-1] };

	# XXX refactor with better variable names
	my &value_str = {
		(("$m_opt={msafe($_)}" if $m_opt),
		("$cfgopts" if $cfgopts)).join(":")
	}

	my $shader = run_cfg(&value_str(%state<value>));
	my @out = %tmp<corrupt>.map({ shader_ssim($_, %tmp<media>, $shader) });
	unlink %tmp{$shader}:delete with %tmp{$shader};
	my $ssim = avgssim(@out.join("\n"));
	@history = [%state<value> => $ssim, |@history];

	if $m_opt {
		stats(&value_str(%state<value>), $ssim, :erronly) if $m_opt;
	} else {
		stats(&value_str(%state<value>), $ssim);
		return %state<value> => $ssim;
	}
	return %state<value> => $ssim if %state<oneshot>;

	if ++%state<depth> >= %state<iter> {
		my Pair $max = &max();
		stats($max.key, $max.value);
		note("maximizer: finished in %state<depth> iterations\n");
		return $max;
	}

	given $prev.value {
	when abs($ssim - $_) < $m_epsilon && !%state<course-change> {
		%state<sandtrap>++;
		%state<step> = 1/%state<step> if $ssim < $_; # estimate best travel direction
		%state<step> = %state<step>**%state<step>;
		%state<value> *= %state<step>**2;

		# XXX maybe make the upper/lower bounds separate parameters?
		if %state<value> < $m_epsilon || %state<value> > 1/$m_epsilon {
			note("maximizer: pointless endeavor");
			stats(&value_str(%state<value>), $ssim);
			return %state<value> => $ssim;
		} else {
			note("maximizer: sandtrap");
			return test($cfgopts, |%state, :history(@history));
		}
	}
	when $ssim > $_ { # just keep going
		%state<value> *= %state<step> *= %state<step>; # XXX should probably scale with SSIM somehow
		return test($cfgopts, |%state, :history(@history));
	}
	default {
		if %state<depth> <= 2 { # handle overestimates
			note("maximizer: changing course");
			%state<course-change> = True;
			%state<step> = 1/%state<step>;
			%state<value> *= %state<step>**2;
			return test($cfgopts, |%state, :history(@history));
		}

		note("maximizer: starting binary search");

		loop {
			my @best_to_worst = @history.sort(*.value).reverse;
			my @values = @history.map(*.key);
			last if abs(@best_to_worst[0].value - @best_to_worst[1].value) < $m_epsilon;

			my $maxval = @best_to_worst[0].key;
			my $lower = @best_to_worst.grep({ $_.key < $maxval })[0].key;
			my $higher = @best_to_worst.grep({ $_.key > $maxval })[0].key;

			# divide until value isn't in history
			my ($low_step, $high_step, $i);
			loop ($i = 1 ;; $i++) {
				$low_step = $lower + ($maxval - $lower) / (2**$i);
				last if not $low_step (elem) @values;
			}
			loop ($i = 1 ;; $i++) {
				$high_step = $maxval + ($higher - $maxval) / (2**$i);
				last if not $high_step (elem) @values;
			}

			my $done = False;
			for ($low_step, $high_step) -> $step {
				@history.push(test($cfgopts, |%state, :value($step), :oneshot));
				last if $done = ++%state<depth> >= %state<iter>;
			}
			last if $done;
		}
		my $max = &max();
		stats(&value_str($max.key), $max.value);
		note("maximizer: finished in %state<depth> iterations\n");
		return $max;
	}
	}
}

# Concatenates or loops files and removes the originals if requested
sub vfconcat(@media, Str :$ext = "concat", Bool :$rm = False, Int() :$loop = 0) {
	my $out-file = "{%gbl<tmp>}.$ext.mkv";

	run <<$ffmpeg>>,
		(<<-stream_loop $loop>> if $loop),
		@media.map({ ('-i', $_) }).flat,
		(<<-lavfi "concat={@media.elems},setpts=N/(FR*TB)">> if @media.elems > 1),
		<<%gbl<outopts> "$out-file">>;

	@media.map(&unlink) if $rm;
	$out-file;
}

# XXX add a "bring your own" corruption where you can specify your own corrupt test media
# XXX maybe measure baseline here?
sub corrupt(Str $corruptions, Bool $no-concat) {
	my Int $realizations = 1;
	my Str @pre-vf;
	my Callable @vf;
	my Str $outopts = %gbl<outopts>;
	for $corruptions.split(',') -> $corruption {
		given $corruption.split('=') {
		when $_[0] eq "NOISE" { # NOISE=NOISE_LEVEL:REALIZATIONS
			my ($noise, $realizations_) = $_[1].split(':');
			$realizations = Int($realizations_);
			@vf.push({ "noise=alls=$noise\:all_seed=$^i\:allf=t" });
		}
		when $_[0] eq "OUTOPTS" { # OUTOPTS=OPTS
			$outopts = $_[1..*].join("=");
		}
		when $_[0] eq "VF" { # VF=VF
			@pre-vf.push($_[1..*].join("="));
		}
		}
	}

	@pre-vf.push("split=$realizations");
	my Str $vf = (
		@pre-vf.join(",") ~ (do "[s$_]" for ^$realizations).join(),
		(do "[s$_]{@vf.map({ $^f($_) }).join(',')}[n$_]" for ^$realizations).join(";\n"),
		(do "[n$_]" for ^$realizations).join() ~ "concat=$realizations,setpts=N/(FR*TB)" if not $no-concat
	).join(";\n");

	$realizations = 1 if not $no-concat;
	%tmp<corrupt> = do "{%gbl<tmp>}.corrupt$_.mkv" for ^$realizations;

	spurt (%tmp<filter_script> = "{%gbl<tmp>}.filter_script"), $vf;
	run <<$ffmpeg -i "%tmp<media>" $outopts -filter_script "{%tmp<filter_script>}">>, %tmp<corrupt>;
}

# recursive ls
sub find(IO::Path() $f --> Array()) { $f.d ?? dir($f).flatmap(&find) !! ($f) }

sub MAIN(
	#= script for testing the SSIM between corrupt->filtered image and the original image
	*@test-media, #= Files/directories to test on
	Str :$cfgopts = "", #= Arguments passed to the dispatched _cfg script, if dispatched
	Str :$shader where *.IO.r = "nlmeans_template", #= Shader used for testing
	Str :$plane = "LUMA", #= Plane to test on
	Str :$corruption = "NOISE=10:10", #= Corruption type, can be NOISE, JPEG, or SCALE
	Str :$vk-device = "vulkan", #= Device name passed to ffmpeg's -init_hw_device option
	Str :$stats-dir where *.IO.d = "stats", #= Directory where stats will be stored
	Str :$max = "", #= Maximize a parameter, syntax goes PARAM:ESTIMATE:STEP_FACTOR:EPSILON:ITERATION_LIMIT
	Str :$pix-fmt = "yuv444p", #= Pixel format used for everything
	Str :$colorspace = "bt709", #= Colorspace used for everything
	Str :$resolution where /^\d+ ':' \d+$/ = "1080:1080", #= Resolution used for everything
	Str :$tmpdir where *.IO.d = '/dev/shm', #= Directory for temporary files
	Bool :$no-stats = False, #= Skip writing to a stats file (still requires a real --stats-dir)
	Bool :$verbose = False, Bool :$v = False, #= Verbose output for debugging
	Bool :$no-concat = False, #= Skip concatenation step, very slow but may be necessary for temporal testing
	Bool :$force = False #= Force overwrite the stats file
) {
	%gbl<shader verbose vk plane resolution> = $shader, ($verbose || $v), $vk-device, $plane, $resolution;
	%gbl<outopts pix-fmt colorspace> = "-c:v ffv1 -colorspace $colorspace -pix_fmt $pix-fmt", $pix-fmt, $colorspace;
	%gbl<max> = roundrobin($max.split(':'), ":10:1.25:1e-6:10".split(':')).map({ $_[0] || $_[1] }).list;

	die 'no test media specified' if not @test-media;

	%gbl<tmpdir> = run(<<mktemp -d "$tmpdir/shader_test.XXXXXX">>, :out).out.slurp(:close).subst("\n");
	%gbl<tmp> = %gbl<tmpdir> ~ '/shader_test.tmp';
	
	# XXX maybe just derive the filename from ARGV instead?
	my @statsfile = (
		# friendly shader name
		%gbl<shader>.IO.basename.subst(/nlmeans/, "NLM").subst(/(_template$|\.glsl$)/),
		# comma delimited basenames of test-media w/o extensions
		@test-media.map({ .IO.basename.split('.', :skip-empty)[0] }).join(','),
		%gbl<plane>, $corruption, %gbl<resolution>.subst(':', 'x'), %gbl<pix-fmt>,
		"CAT={(!$no-concat).Int}", "MAX={%gbl<max>.join(':')}", $cfgopts
	);
	my $statsfile = "$stats-dir/{@statsfile.join(':').trans('/' => '_')}.stats";

	@test-media = @test-media.flatmap(&find).grep({ $_.path !~~ /\.txt$/ });
	@test-media.map({ .f || die "$_ doesn't exist or is unreadable" });

	run <<renice -n 19 "$*PID">>; run <<ionice -c 3 -p "$*PID">>;
	if not $no-stats {
		if $force {
			%gbl<statsfile> = open $statsfile, :w;
		} else {
			die 'stats file exists' if "$statsfile.bz2".IO.e;
			try { %gbl<statsfile> = open $statsfile, :x; }
			die 'stats file exists' if $!;
		}
	}
	note('Encoding test media...');
	%tmp<media> = vfconcat(@test-media);

	note('Corrupting test media...');
	corrupt($corruption, $no-concat);

	note('Measuring baseline...');
	# XXX baseline should be generated from libplacebo upscaling
	my $baseline = avgssim(%tmp<corrupt>.map({
		run(<<$ffmpeg -i "$_" -i "%tmp<media>" -lavfi "scale={%gbl<resolution>},ssim=-" {%gbl<outopts>} -f null ->>, :out).out.slurp(:close)
	}).join("\n"));
	stats "BASELINE=$corruption", $baseline;

	my @flatopts = $cfgopts.split(/<[=:]>/, :skip-empty); # parse k=v:k=v:k=v:...
	die 'invalid cfgopts' if @flatopts.elems && not @flatopts.elems %% 2;
	my @opts = @flatopts[0,2...*].unique(:with({ $^x === $^y && die 'duplicate opt' }));
	my @values = @flatopts[1,3...*].map(&EVAL);
	my @cfgopts = do given @opts.elems {
		when 0 { ("") }
		when 1 { @values[0].map({ "@opts[0]=$_" }) }
		default {
			[X] @values ==> map({ @opts Z $_ }) # generate permutations
			            ==> map({ $_>>.map(&msafe)>>.join('=').join(':') }) # stringify
		}
	}

	note("Trying {@cfgopts.elems || 1} permutations...");
	note(@cfgopts.raku) if %gbl<verbose>;

	@cfgopts.map(&test);

	if %gbl<statsfile> {
		%gbl<statsfile>.close();
		note("\nSorted worst to best:\n");
		run(<sort -k 2 -n>, :in(open %gbl<statsfile>.IO.path), :out)
			.out.slurp(:close).split("\n")
			==> { .elems < 15 ?? $_ !! $_[*-15..*] }() # last 15 or less
			==> join("\n") ==> note();

		run <bzip2 -9>, ('-f' if $force), %gbl<statsfile>.IO.path;
	}
}

